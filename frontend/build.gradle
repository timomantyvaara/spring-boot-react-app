// https://github.com/srs/gradle-node-plugin
// installs node, npm etc. and enables executing node scripts from gradle
plugins {
    id "com.moowork.node" version "1.3.1"
}
apply plugin: 'base'

version = '0.0.1-SNAPSHOT'

node {
    version = '10.15.3'
    npmVersion = '6.4.1'
    download = true
}

// override with npm install with no audit
task npm_install(type: NpmTask) {
    args = ['install', '--no-audit']
}

task forceClean(type: NpmTask) {
    args = ['cache', 'clean', '--force']
}

task startFront(type: NpmTask) {
    dependsOn npm_install
    args = ['start']
}

// produces production ready build from react app
task bundleFront(type: NpmTask) {
    dependsOn npm_install
    args = ['run', 'build']
}

// packages production ready files into a jar to be included in backend application package
task frontendJar(type: Zip) {
    dependsOn bundleFront

    baseName 'frontend'
    extension 'jar'

    from ('build') {
        exclude 'distributions/**'
        into 'static'
    }

    includeEmptyDirs = false
}